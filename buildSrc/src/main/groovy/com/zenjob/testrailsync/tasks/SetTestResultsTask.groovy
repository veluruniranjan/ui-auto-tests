package com.zenjob.testrailsync.tasks

import com.zenjob.testrailsync.executors.TestRailResultPusher
import com.zenjob.testrailsync.obj.TestCaseResult
import groovy.io.FileType
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.TaskAction
import org.gradle.api.tasks.options.Option

class SetTestResultsTask extends DefaultTask {

    // Default Test run id (ZVS project)
    @Input
    Long runId = 136

    @Option(option = "runId", description = "Sets TestRail test run id, where results will be pushed to")
    void setRunId(String runId) {
        this.runId = Long.valueOf(runId)
    }

    @TaskAction
    void setTestResults() {
        TestRailSyncPluginExtension config = project.extensions.testRailSync
        TestRailResultPusher pusher = new TestRailResultPusher()

        println('Pushing results to TestRail')

        assert config.reportsPath: "Cucumber reports path isn't set"

        //Directory with reports generated by cucumber reporting default plugin
        def reportsDir = new File(config.reportsPath)
        List<TestCaseResult> results = new ArrayList<>()
        reportsDir.eachFileRecurse(FileType.FILES) { File file ->
            results += pusher.parseResultsFromJson(config.reportsPath, file)
        }
        if (results.isEmpty()) {
            throw new Exception("There are no results to push from path ${config.reportsPath}")
        }

        println("Results are posted in $runId test run")

        pusher.pushResults(results, runId)
    }
}
